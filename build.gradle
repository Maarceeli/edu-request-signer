plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.7.20'
    id 'maven-publish'
}

allprojects {
    apply plugin: 'java'

    version = "0.1.0-SNAPSHOT"
    group = "io.github.wulkanowy"

    repositories {
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
}

subprojects {
    apply plugin: 'jacoco'

    sourceCompatibility = 11

    compileKotlin {
        kotlinOptions {
            jvmTarget = "11"
            javaParameters = true
        }
    }

    dependencies {
        implementation "com.brsanthu:migbase64:2.2"
        testImplementation "junit:junit:4.13"
    }

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    test {
        testLogging.showStandardStreams = false
    }

    group = "io.github.wulkanowy.signer"
    if (project.plugins.hasPlugin('java')) {
        task sourcesJar(type: Jar, dependsOn: classes) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }
        task javadocJar(type: Jar, dependsOn: javadoc) {
            classifier = 'javadoc'
            from javadoc.destinationDir
        }
        artifacts {
            archives sourcesJar
            archives javadocJar
        }
    }
}

dependencies {
    implementation project(":hebe-jvm")
    implementation project(":jvm")
}
