apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

ext {
    PUBLISH_VERSION = '0.1.0'
    SITE_URL = 'https://github.com/wulkanowy/uonet-request-signer'
    GIT_URL = 'https://github.com/wulkanowy/uonet-request-signer.git'
}

group 'io.github.wulkanowy'
version PUBLISH_VERSION

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 31
        versionCode 1
        versionName PUBLISH_VERSION

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation "com.brsanthu:migbase64:2.2"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact androidSourcesJar

                groupId = group
                artifactId "uonet-request-signer-hebe-android"
                version = android.defaultConfig.versionName
                pom {
                    name = 'UONET+ (hebe) request signer for Android'
                    url = SITE_URL
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'kuba2k2'
                            name = 'Kuba Szczodrzy≈Ñski'
                            email = 'kuba@szczodrzynski.pl'
                        }
                    }
                    scm {
                        connection = GIT_URL
                        developerConnection = GIT_URL
                        url = SITE_URL
                    }
                }
            }
        }
    }
}
